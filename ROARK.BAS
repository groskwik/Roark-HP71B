5 ENG 4
10 DELAY 1.0 @ DISP "*** ROARK'S FORMULA ***" @ DELAY 5
20 DESTROY ALL 
30 INPUT "Case:","2e";C$
40 C1$=C$[1,1] @ C2$=UPRC$(C$[2,2])
50 INPUT "L:","2000";L
60 INPUT "a:","200";A
70 INPUT "E:","200000";E
80 INPUT "I:","6670000";I @ E1=E*I
90 IF C1$="1" THEN GOTO 110
100 IF C1$="2" THEN GOTO 120 ELSE 900
110 INPUT "W:","1000";W @ GOTO 140
120 INPUT "wa:","0.1";W
130 INPUT "wl:","1.0";W1
140 INPUT "Step:","100";Q
150 DEF FNP(X)=(X>A)*(X-A)
160 DEF FNO(X)
170 IF X>A THEN FNO=1 ELSE FNO=0
180 END DEF 
190 GOTO UPRC$("C"&C$)
200 'C1A': R1=0 @ M1=0 @ T1=W*(L-A)^2/(2*E1)
210 Y1=-W/(6*E1)*(2*L^3-3*L^2*A+A^3) @ GOTO "RES1"
220 'C1B': R1=0 @ M1=W*(L-A)^2/(2*L) @ T1=0
230 Y1=-W/(12*E1)*(L-A)^2*(L+2*A) @ GOTO "RES1"
240 'C1C': R1=W/(2*L^3)*(L-A)^2*(2*L+A) @ M1=0
250 T1=-W*A/(4*E1*L)*(L-A)^2 @ Y1=0 @ GOTO "RES1"
260 'C1D': R1=W/L^3*(L-A)^2*(L+2*A) @ M1=-W*A/L^2*(L-A)^2
270 T1=0 @ Y1=0 @ GOTO "RES1"
280 'C1E': R1=W/L*(L-A) @ M1=0 @ Y1=0
290 T1=-W*A/(6*E1*L)*(2*L-A)*(L-A) @ GOTO "RES1"
300 'C1F': R1=0 @ M1=W*(L-A) @ T1=0
310 Y1=-W*(L-A)/(6*E1)*(2*L^2+2*A*L-A^2) @ GOTO "RES1"
320 'C2A': R1=0 @ M1=0 @ T1=-W*(L-A)^3/(6*E1)+(W1-W)/(24*E1)*(L-A)^3
330 'C2A': R1=0 @ M1=0 @ T1=-W*(L-A)^3/(6*E1)+(W1-W)/(24*E1)*(L-A)^3
340 Y1=-W/(24*E1)*(L-A)^3*(3*L+A)-(W1-W)/(120*E1)*(L-A)^3*(4*L+A) @ GOTO "RES2"
350 'C2B': R1=0 @ M1=W*(L-A)^3/(6*L)+(W1-W)/(24*L)*(L-A)^3 @ T1=0
360 Y1=-W/(24*E1)*(L-A)^3*(L+A)-(W1-W)/(240*E1)*(L-A)^3*(3*L+2*A) @ GOTO "RES2"
370 'C2C': R1=W/(8*L^3)*(L-A)^3*(3*L+A)+(W1-W)/(40*L^3)*(L-A)^3*(4*L+A) @ M1=0
380 T1=-W/(48*E1*L)*(L-A)^3*(L+3*A)-(W1-W)/(240*E1*L)*(L-A)^3*(2*L+3*A) @ Y1=0 @ GOTO "RES2"
390 'C2D': R1=W/(2*L^3)*(L-A)^3*(L+A)+(W1-W)/(20*L^3)*(L-A)^3*(3*L+2*A) @ T1=0 @ Y1=0
400 M1=-W/(12*L^2)*(L-A)^3*(L+3*A)-(W1-W)/(60*L^2)*(L-A)^3*(2*L+3*A) @ GOTO "RES2"
410 'C2E': R1=W/(2*L)*(L-A)^2+(W1-W)/(6*L)*(L-A)^2 @ M1=0 @ Y1=0
420 T1=-W/(24*E1*L)*(L-A)^2*(L^2+2*A*L-A^2)-(W1-W)/(360*E1*L)*(L-A)^2*(7*L^2+6*A*L-3*A^2)
430 GOTO "RES2"
440 'C2F': R1=0 @ M1=W/2*(L-A)^2+(W1-W)/6*(L-A)^2 @ T1=0
450 Y1=-W*(L-A)^2/(24*E1)*(5*L^2+2*A*L-A^2)-(W1-W)/(120*E1)*(L-A)^2*(9*L^2+2*A*L-A^2)
460 GOTO "RES2"
470 X1=FNROOT(0,L/2,FNT2(FVAR)) @ Y1=FNY2(X1)
480 'C1': DEF FNV1(X)=R1-W*FNO(X)
490 DEF FNM1(X)=M1+R1*X-W*FNP(X)
500 DEF FNT1(X)=T1+M1*X/E1+R1*X^2/(2*E1)-W/(2*E1)*FNP(X)^2
510 DEF FNY1(X)
520 FNY1=Y1+T1*X+M1*X^2/(2*E1)+R1*X^3/(6*E1)-W/(6*E1)*FNP(X)^3
530 END DEF 
540 'C2': DEF FNV2(X)=R1-W*FNP(X)-(W1-W)/(2*(L-A))*FNP(X)^2
550 DEF FNM2(X)=M1+R1*X-W/2*FNP(X)^2-(W1-W)/(6*(L-A))*FNP(X)^3
560 DEF FNT2(X)=T1+M1*X/E1+R1*X^2/(2*E1)-W/(6*E1)*FNP(X)^3-(W1-W)/(24*E1*(L-A))*FNP(X)^4
570 DEF FNY2(X)
580 FNY2=Y1+T1*X+M1*X^2/(2*E1)+R1*X^3/(6*E1)-W/(24*E1)*FNP(X)^4-(W1-W)/(120*E1*(L-A))*FNP(X)^5
590 END DEF 
600 'RES1': IF C2$="A" OR C2$="B" THEN X0=L ELSE GOTO 602
602 IF C2$="F" THEN X0=0 ELSE X0=FNROOT(.05*L,L*.95,FNV1(FVAR))
605 M0=FNM1(X0)
610 IF C2$="A" OR C2$="B" THEN X1=0 ELSE X1=FNROOT(.05*L,L*.95,FNT1(FVAR))
615 Y2=FNY1(X1)
620 R2=-FNV1(L) @ M2=FNM1(L) @ GOTO 660
630 'RES2': IF C2$="A" OR C2$="B" THEN X0=L ELSE X0=FNROOT(.05*L,L*.95,FNV2(FVAR))
635 M0=FNM2(X0)
640 IF C2$="A" OR C2$="B" THEN X1=0 ELSE X1=FNROOT(.05*L,L*.95,FNT2(FVAR))
645 Y2=FNY2(X1)
650 R2=-FNV2(L) @ M2=FNM2(L)
655 DISP "REACTIONS AT SUPPORTS"
660 CALL DISPRES(R1,"RA")
670 CALL DISPRES(R2,"RB")
680 CALL DISPRES(M1,"MA")
690 CALL DISPRES(M2,"MB")
710 DISP "**  MAXIMAL VALUES **"
715 ENG 2
720 DISP "X:";X0;" M:";M0
730 DISP "X:";X1;" Y:";Y2
735 ENG 4
740 DIM T$(3),H$(3),Z(3)
750 T$(1)=" ****  DEFLECTION ****" @ T$(2)=" *****  MOMENT *****"
755 T$(3)="***  SHEAR FORCE  ****"
760 H$(1)=" X          Y(X)" @ H$(2)=" X          M(X)" @ H$(3)=" X          V(X)"
770 FOR I=1 TO 3
780 DISP T$(I)
790 DISP H$(I)
800 FOR X=0 TO L+Q STEP Q
810 IF X>L THEN X=L
820 IF C1$="1" THEN Z(1)=FNY1(X) ELSE Z(1)=FNY2(X)
830 IF C1$="1" THEN Z(2)=FNM1(X) ELSE Z(2)=FNM2(X)
840 IF C1$="1" THEN Z(3)=FNV1(X) ELSE Z(3)=FNV2(X)
850 DISP X;" ";Z(I)
860 IF X=L THEN X=L+Q
870 NEXT X
880 NEXT I
885 STD 
890 END 
910 SUB DISPRES(Z,S$)
920 IF ABS(Z)>0 THEN DISP S$;":";Z
930 END SUB 
